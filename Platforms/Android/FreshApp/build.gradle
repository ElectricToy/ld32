apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.1'
    defaultConfig {
        applicationId "co.electrictoy.ld32"
        minSdkVersion 9
        targetSdkVersion 19
        versionCode 8
        versionName "1.0.8"

    }
    buildTypes {
        release {
            ndk {
                moduleName "ld32"
                abiFilter "armeabi-v7a"
                stl "gnustl_static"
            }
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    sourceSets.main {
        java.srcDirs = ['src', '../../../../Fresh/Platforms/Android/src']
        jni.srcDirs = []
        jniLibs.srcDir 'src/main/libs'
        assets.srcDirs = ['../../../assets']
    }

    task updateVer(type:Exec, description: 'Updating version file') {
        workingDir '..'
        commandLine './upver.sh'
    }

    task buildNative(type: Exec, description: 'Compile JNI source via NDK') {
        def ndkDir = project.plugins.findPlugin('com.android.application').getNdkFolder()
        commandLine "$ndkDir/ndk-build",
                '-C', file('src/main/jni').absolutePath,
                '-j', Runtime.runtime.availableProcessors(),
                'all',
                'NDK_DEBUG=1'
    }

    task cleanNative(type: Exec, description: 'Clean JNI object files') {
        def ndkDir = project.plugins.findPlugin('com.android.application').getNdkFolder()
        commandLine "$ndkDir/ndk-build",
                '-C', file('src/main/jni').absolutePath,
                'clean'
    }

    buildNative.dependsOn 'updateVer'
    clean.dependsOn 'cleanNative'

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn buildNative
    }
}

dependencies {
    compile 'com.android.support:support-v4:20.0.0'
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.2+'
    }
}
